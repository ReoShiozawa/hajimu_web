取り込む "hajimu_web" として ウェブ

関数 認証ミドルウェア(リクエスト):
  変数 メソッド = リクエスト[0]
  変数 パス = リクエスト[1]
  表示("  [認証MW] " + メソッド + " " + パス)
  返す 真
終わり

関数 APIログ(リクエスト):
  変数 パス = リクエスト[1]
  表示("  [API専用MW] " + パス)
  返す 真
終わり

関数 ハロー処理(リクエスト):
  変数 ホスト = リクエスト[13]
  変数 プロトコル = リクエスト[14]
  変数 安全 = リクエスト[15]
  ウェブ.JSON送信([["message", "こんにちは v5.0！"], ["host", ホスト], ["protocol", プロトコル], ["secure", 安全]])
  返す ""
終わり

関数 ユーザー処理(リクエスト):
  変数 パラメータ = リクエスト[6]
  変数 ユーザーID = パラメータ["id"]
  ウェブ.JSON送信([["user_id", ユーザーID], ["name", "太郎"]])
  返す ""
終わり

関数 オプショナル処理(リクエスト):
  変数 パラメータ = リクエスト[6]
  変数 カテゴリ = パラメータ["category"]
  変数 ページ = パラメータ["page"]
  ウェブ.JSON送信([["category", カテゴリ], ["page", ページ]])
  返す ""
終わり

関数 リダイレクト処理(リクエスト):
  ウェブ.リダイレクト("/api/hello")
  返す ""
終わり

関数 ヘッダーテスト(リクエスト):
  変数 ヘッダー = リクエスト[5]
  変数 ホスト = ヘッダー["Host"]
  変数 UA = ヘッダー["User-Agent"]
  ウェブ.JSON送信([["host", ホスト], ["user_agent", UA]])
  返す ""
終わり

関数 JSON解析テスト(リクエスト):
  変数 データ = ウェブ.JSON解析("{\"name\":\"太郎\",\"age\":25,\"active\":true}")
  変数 名前 = データ["name"]
  変数 年齢 = データ["age"]
  変数 活動 = データ["active"]
  ウェブ.JSON送信([["parsed_name", 名前], ["parsed_age", 年齢], ["parsed_active", 活動]])
  返す ""
終わり

関数 バリデーション処理(リクエスト):
  変数 r1 = ウェブ.バリデーション("hello", "required")
  変数 r2 = ウェブ.バリデーション("", "required")
  変数 r3 = ウェブ.バリデーション("test@example.com", "email")
  変数 r4 = ウェブ.バリデーション("https://example.com", "url")
  変数 r5 = ウェブ.バリデーション("42", "number")
  ウェブ.JSON送信([["required_ok", r1], ["required_empty", r2], ["email_ok", r3], ["url_ok", r4], ["number_ok", r5]])
  返す ""
終わり

関数 セッション設定処理(リクエスト):
  ウェブ.セッション設定("username", "テストユーザー")
  ウェブ.JSON送信([["status", "session_set"]])
  返す ""
終わり

関数 セッション取得処理(リクエスト):
  変数 名前 = ウェブ.セッション取得("username")
  ウェブ.JSON送信([["username", 名前]])
  返す ""
終わり

関数 エラーハンドラ1(コード, メッセージ, リクエスト):
  返す "<h1>カスタムエラー: " + メッセージ + "</h1>"
終わり

ウェブ.サーバー作成(8090)

ウェブ.ミドルウェア("logger")
ウェブ.ミドルウェア("cors")
ウェブ.ミドルウェア("json")
ウェブ.ミドルウェア("session")

ウェブ.使用(認証ミドルウェア)
ウェブ.使用("/api", APIログ)

ウェブ.エラーハンドラ(エラーハンドラ1)

ウェブ.GET("/api/hello", ハロー処理)
ウェブ.GET("/api/users/:id", ユーザー処理)
ウェブ.GET("/api/items/:category/:page?", オプショナル処理)
ウェブ.GET("/go", リダイレクト処理)
ウェブ.GET("/headers", ヘッダーテスト)
ウェブ.GET("/json-parse", JSON解析テスト)
ウェブ.GET("/validate", バリデーション処理)
ウェブ.GET("/session/set", セッション設定処理)
ウェブ.GET("/session/get", セッション取得処理)

ウェブ.キープアライブ(真, 15, 100)
ウェブ.信頼プロキシ(真)

ウェブ.起動()
